/**
 * S2Util Library
 *
 * Copyright 2020 - 2026 devers2 (이승수, Daejeon, Korea)
 * Contact: eseungsu.dev@gmail.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * For more information, please see the LICENSE file in the root directory.
 */

ext {
    /*
     * [추가 소스 목록]
     * dynamicSourceInfoMap에 정의된 기능 키(예: 'licensesInfo')를 추가하여 관련된 소스 파일 및 라이브러리 의존성을 빌드에 자동으로 포함시킬 수 있다.
     */
    activeFeatures = ['licensesInfo'] as Set

    /**
     * [동적 기능 소스 정보 (Feature Toggles)]
     * - 특정 기능(Feature)에 포함될 소스 파일과 라이선스 정보 정의
     */
    dynamicSourceInfoMap = [
        'licensesInfo': [
            licenses: [
                'README.md',
                'LICENSE',
                'licenses/LICENSE-APACHE-2.0',
                'licenses/NOTICE'
            ]
        ]
    ]

    /*
     * 빌드 완료 후 결과물을 테스트할 클래스 지정 (shadowJar 실행 후 testArtifact 태스크로 수행됨, 여러 개 지정 가능)
     */
    artifactTestClassNames = ["io.github.devers2.s2util.validation.SmokeTest"]
}

dependencies {
    /**
     * implementation: 컴파일 및 런타임 시 모두 사용함
     * - 해당 의존성이 프로젝트의 빌드 결과물(JAR)에 직접적인 영향을 미침
     * - 이 라이브러리를 사용하는 다른 프로젝트(상위 모듈)에는 의존성이 노출되지 않음 (API 캡슐화)
     * - 런타임에 반드시 필요한 라이브러리인 경우 이 방식을 사용함
     */
    implementation project(':s2-core')

    /**
     * compileOnly: 컴파일 시 사용함
     * - 런타임 시 사용하지 않는 라이브러리인 경우 이 방식을 사용함
     * - Shadow JAR 생성 시 포함되지 않음
     */
    compileOnly(libs.spring6.context) // Java 17 이상으로 개발하므로 Spring 6 및 Spring Boot 3 계열이 표준

    /**
     * testImplementation: 테스트 컴파일 및 런타임 시 모두 사용
     * - 테스트 코드에서 사용하는 라이브러리인 경우 이 방식을 사용
     */
    testImplementation(libs.spring6.context)
    testImplementation(libs.junit.jupiter)
    testRuntimeOnly(libs.junit.platform.launcher)
}

/**
 * [Gradle 빌드 최적화 및 태스크 의존성 설정]
 * Gradle 9.x 이상에서는 태스크 간의 암시적 의존성을 허용하지 않습니다.
 * :s2-validator의 작업들이 :s2-core에서 생성된 JAR 파일을 참조하므로,
 * 실행 순서와 입출력 관계를 명시적으로 정의해야 빌드 오류를 방지할 수 있습니다.
 */

// 1. 표준 jar 태스크 설정
tasks.named('jar') {
    // s2-core의 jar 결과물을 이 태스크의 입력값으로 명시하여 변경 사항을 감지하도록 함 (Lazy Configuration)
    inputs.file(project(':s2-core').tasks.named('jar').map { it.archiveFile })

    // s2-core 프로젝트의 jar 태스크가 완료된 후 실행되도록 보장
    dependsOn(':s2-core:jar')
}

// 2. shadowJar 태스크 설정 (플러그인에 의해 동적으로 생성될 수 있으므로 matching 사용)
tasks.matching { it.name == 'shadowJar' }.all {
    // shadowJar 생성 시 s2-core의 최신 JAR 파일이 필요함을 명시
    dependsOn(':s2-core:jar')
}

// 3. 라이선스 관련 태스크 설정
// 로그상에서 발생한 'implicit dependency' 에러를 해결하기 위해 추가
tasks.matching { it.name == 'downloadLicenses' }.all {
    // 라이선스 정보를 추출하기 위해 s2-core의 아티팩트를 읽어야 하므로 선행 작업으로 등록
    dependsOn(':s2-core:jar')
}
