/**
 * S2Util Library
 *
 * Copyright 2020 - 2026 devers2 (ì´ìŠ¹ìˆ˜, Daejeon, Korea)
 * Contact: eseungsu.dev@gmail.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * For more information, please see the LICENSE file in the root directory.
 */

/*
 * [Version Catalog]
 * ì˜ì¡´ì„± ë° í”ŒëŸ¬ê·¸ì¸ ë²„ì „ì€ 'gradle/libs.versions.toml' íŒŒì¼ì—ì„œ í†µí•© ê´€ë¦¬
 * ë³„ë„ ì„¤ì • ì—†ì´ Gradleì´ ê¸°ë³¸ ê²½ë¡œ(gradle/libs.versions.toml)ë¥¼ ìë™ìœ¼ë¡œ ì¸ì‹í•˜ì—¬ 'libs' ì ‘ê·¼ìë¡œ ì œê³µ
 */
plugins {
    /* ë²„ì „ ì¹´íƒˆë¡œê·¸ ì‚¬ìš© (libs.versions.toml) */
    alias(libs.plugins.shadow) // â­ 'shadow' í”ŒëŸ¬ê·¸ì¸ì´ ì ìš©ë˜ê³  shadedPackagePrefix ì„ ì„¤ì •í•œ ê²½ìš°, ìë™ìœ¼ë¡œ Shadow JAR ë¥¼ ìƒì„±í•˜ë„ë¡ êµ¬ì„±ëœë‹¤. (implementation, runtimeOnly ì˜ì¡´ì„±ì€ relocate ì²˜ë¦¬)
    alias(libs.plugins.s2.build.support)
    alias(libs.plugins.license) apply false // â­ 'license' í”ŒëŸ¬ê·¸ì¸ (hierynomus) ë²„ì „ì„ ì¹´íƒˆë¡œê·¸ì—ì„œ ê´€ë¦¬í•˜ê³ , í•˜ìœ„ í”„ë¡œì íŠ¸ì—ì„œ IDë¡œ ì ìš© ê°€ëŠ¥í•˜ê²Œ í•¨
    id 'signing'
}

import io.github.devers2.buildsupport.S2BuildUtils

/*
 * Maven ì¢Œí‘œ ì„¤ì • (Root)
 * - group: í”„ë¡œì íŠ¸ì˜ ê·¸ë£¹ ID (ì˜ˆ: io.github.devers2, com.example)
 * - version: í”„ë¡œì íŠ¸ ë²„ì „
 *
 * â—ì¤‘ìš”: ë™ì¼í•œ ì†ŒìŠ¤ë¥¼ ë‹¤ë¥¸ ì¡°ì§/ëª©ì ìœ¼ë¡œ ë°°í¬í•˜ëŠ” ê²½ìš° groupì„ ë‹¤ë¥´ê²Œ ì„¤ì •í•´ì•¼ í•œë‹¤.
 *   ì˜ˆì‹œ) ì›ë³¸: io.github.devers2, í¬í¬: com.company
 *   ì´ë ‡ê²Œ í•˜ë©´ ì˜ì¡´ì„± ê´€ë¦¬ ë„êµ¬ê°€ ì„œë¡œ ë‹¤ë¥¸ ì•„í‹°íŒ©íŠ¸ë¡œ ì¸ì‹í•˜ì—¬ ê°™ì€ ë¦¬í¬ì§€í† ë¦¬ë¼ë„ ë³„ë„ ì•„í‹°íŒ©íŠ¸ë¡œ ì·¨ê¸‰ëœë‹¤.
 */
group = 'io.github.devers2'
version = '1.0.0'

// ì „ì—­ ë³€ìˆ˜ ì„ ì–¸
ext {
    // GitHub Packages ë¦¬í¬ì§€í† ë¦¬ ì •ë³´ (devers2/s2-packages)
    REPO_OWNER = 'devers2'
    REPO_NAME = 's2-util'

    // Shadow Plugin - Relocation ëŒ€ìƒ íŒ¨í‚¤ì§€ ì„¤ì •(ë¯¸ì„¤ì • ì‹œ Relocationì´ ì ìš©ë˜ì§€ ì•ŠìŒ)
    // shadedPackagePrefix = "io.github.devers2.s2util.shaded"



    // ========================================================================
    // â­ [ì‚¬ìš©ì ì„¤ì • (User Configuration)]
    // ê°œë°œìê°€ í”„ë¡œì íŠ¸ ìƒí™©ì— ë§ì¶° ìì£¼ ë³€ê²½í•˜ê±°ë‚˜ í™•ì¸í•´ì•¼ í•˜ëŠ” ì„¤ì •
    // ========================================================================

    /**
     * ê¸°ë³¸ Java ë²„ì „ ì„¤ì •: JavaVersion.current() ë˜ëŠ” JavaVersion.VERSION_17, JavaVersion.VERSION_25 ë“± ì„¤ì • ê°€ëŠ¥
     * (JavaVersion.current(): í˜„ì¬ ì‹¤í–‰ ì¤‘ì¸ JVM ë²„ì „)
     *
     * Java ë²„ì „ì— ë”°ë¥¸ ì˜ì¡´ì„± ì„¤ì •
     * JavaVersion.VERSION_10 ì´í•˜: javax.servlet
     * JavaVersion.VERSION_11 ì´ìƒ: jakarta.servlet
     *
     * Jakarta EE ë²„ì „   ì„œë¸”ë¦¿ ë²„ì „     íŒ¨í‚¤ì§€ ëª…ì¹­        ì¼ë°˜ì ìœ¼ë¡œ ì‚¬ìš©ë˜ëŠ” Java ë²„ì „
     * -----------------------------------------------------------------------------
     * Jakarta EE 8      Servlet 4.0    javax.servlet      Java 8, Java 11 ë“±
     * Jakarta EE 9      Servlet 5.0    jakarta.servlet    Java 11, Java 17 ë“±
     * Jakarta EE 10     Servlet 6.0    jakarta.servlet    Java 17, Java 21 ë“±
     */
    javaVersion = JavaVersion.VERSION_21

    /*
     * [ì¶”ê°€ ì†ŒìŠ¤ ëª©ë¡]
     * dynamicSourceInfoMapì— ì •ì˜ëœ ê¸°ëŠ¥ í‚¤(ì˜ˆ: 'S2PdfUtil')ë¥¼ ì¶”ê°€í•˜ì—¬ ê´€ë ¨ëœ ì†ŒìŠ¤ íŒŒì¼ ë° ë¼ì´ë¸ŒëŸ¬ë¦¬ ì˜ì¡´ì„±ì„ ë¹Œë“œì— ìë™ìœ¼ë¡œ í¬í•¨ì‹œí‚¬ ìˆ˜ ìˆë‹¤.
     */
    activeFeatures = [] as Set

    /**
     * [ë™ì  ê¸°ëŠ¥ ì†ŒìŠ¤ ì •ë³´ (Feature Toggles)]
     * - íŠ¹ì • ê¸°ëŠ¥(Feature)ì— í¬í•¨ë  ì†ŒìŠ¤ íŒŒì¼ê³¼ ë¼ì´ì„ ìŠ¤ ì •ë³´ ì •ì˜
     *
     * ğŸ’¡ ì´ ì„¤ì •ì€ ê° ì„œë¸Œí”„ë¡œì íŠ¸ì˜ build.gradle ì—ì„œ ëª¨ë“ˆë³„ë¡œ ê´€ë¦¬ëœë‹¤.
     * [ì‚¬ìš© ì˜ˆì‹œ - subproject/build.gradle]
     * ext {
     *     activeFeatures = ['S2PdfUtil'] // í™œì„±í™”í•  ê¸°ëŠ¥ í‚¤
     *     dynamicSourceInfoMap = [
     *         'S2PdfUtil': [
     *             variantId: 'pdf',
     *             sources: ['io/github/devers2/s2util/support/S2PdfUtil.java'],
     *             dependencies: [
     *                 [configuration: 'compileOnly', group: 'org.jsoup', name: 'jsoup', version: '1.18.3']
     *             ],
     *             licenses: ['README-LGPL-2.1-PDF.md']
     *         ]
     *     ]
     * }
     */
    dynamicSourceInfoMap = [:]

    /*
     * ê¸°ë³¸ ì œì™¸ ì†ŒìŠ¤ ëª©ë¡ (í•­ìƒ ì œì™¸ë˜ëŠ” íŒŒì¼ë“¤)
     */
    excludedSources = [] as Set

    /**
     * [ê¸°ì¤€ Java ë²„ì „: Artifact ID ìƒì„± ê¸°ì¤€]
     * javaVersionì´ baselineJavaVersionê³¼ ë‹¤ë¥¼ ê²½ìš° ì•„í‹°íŒ©íŠ¸ IDì— ì ‘ë¯¸ì‚¬ ì¶”ê°€
     */
    baselineJavaVersion = JavaVersion.VERSION_21

    /**
     * ì•ˆì „í•œ íƒœìŠ¤í¬ ëª©ë¡ (ë¡œì»¬ ë¹Œë“œ/í…ŒìŠ¤íŠ¸ìš©)
     * - ì´ íƒœìŠ¤í¬ ì‹¤í–‰ ì‹œì—ëŠ” ì†ŒìŠ¤ JARë¥¼ ìƒì„±í•´ë„ ì•ˆì „í•˜ë‹¤ê³  íŒë‹¨
     */
    safeTasks = ['assemble', 'build', 'jar', 'sourcesJar', 'publishToMavenLocal'] as Set



    // ========================================================================
    // â­ [ìƒìˆ˜ ë° í™˜ê²½ ì„¤ì • (Constants & Environment)]
    // í”„ë¡œì íŠ¸ êµ¬ì¡°ë‚˜ ì™¸ë¶€ í™˜ê²½ê³¼ ê´€ë ¨ëœ ì„¤ì • (ë³€ê²½ ë¹ˆë„ ë‚®ìŒ)
    // ========================================================================

    JAVA_SRC_ROOT = 'src/main/java'
    RESOURCES_SRC_ROOT = 'src/main/resources'

    // GitHub Packages URL (.git ì œì™¸)
    REPO_BASE_URL = "https://maven.pkg.github.com/${REPO_OWNER}/${REPO_NAME}"
    GITHUB_USER = project.findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
    GITHUB_TOKEN = project.findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
}



// ========================================================================
// â­ [ë¹Œë“œ ë¡œì§ (Build Logic & Calculations)]
// ìœ„ ì„¤ì •ê°’ë“¤ì„ ê¸°ë°˜ìœ¼ë¡œ ì‹¤ì œ ë¹Œë“œì— í•„ìš”í•œ ê°’ ê³„ì‚° ë° ìœ í‹¸ë¦¬í‹° í˜¸ì¶œ
// â€» íŠ¹ë³„í•œ ì´ìœ ê°€ ì—†ë‹¤ë©´ ìˆ˜ë™ ìˆ˜ì • ì§€ì–‘
// ========================================================================

/*
 * [Java ë²„ì „ ì˜¤ë²„ë¼ì´ë“œ]
 * -PtargetJavaVersion=21 ì˜µì…˜ìœ¼ë¡œ ë®ì–´ì“°ê¸° ê°€ëŠ¥
 */
if (project.hasProperty('targetJavaVersion')) {
    project.ext.javaVersion = JavaVersion.toVersion(project.findProperty('targetJavaVersion'))
}

/*
 * [ë™ì  ê¸°ëŠ¥ ëª¨ë“ˆ ì˜¤ë²„ë¼ì´ë“œ]
 * -PtargetSources=S2PdfUtil,OtherFeature ì˜µì…˜ìœ¼ë¡œ ë®ì–´ì“°ê¸° ê°€ëŠ¥
 */
if (project.hasProperty('targetSources')) {
    def targetSourcesVal = project.findProperty('targetSources')
    def sources = targetSourcesVal.toString().split(',')
    project.ext.activeFeatures = sources.collect { it.trim() } as Set
}




// --------------------------------------------------------------------------------------
// [Dynamic Artifact ID ì„¤ì •] ê¸°ë³¸ Java ë²„ì „ê³¼ ë‹¤ë¥´ê±°ë‚˜ ì¶”ê°€ ì†ŒìŠ¤ê°€ ìˆëŠ” ê²½ìš° ì ‘ë¯¸ì‚¬ ì¶”ê°€
// --------------------------------------------------------------------------------------
def artifactSuffix = ""

// 1. Java ë²„ì „ ì²´í¬
if (project.ext.javaVersion != project.ext.baselineJavaVersion) {
    // Java MAJOR ë²„ì „ë§Œ ì¶”ì¶œ (ì˜ˆ: 1.8 -> 8, 11 -> 11)
    artifactSuffix += "-java${project.ext.javaVersion.majorVersion}"
}

// 2. ì¶”ê°€ ì†ŒìŠ¤ ì²´í¬ (variantId ì‚¬ìš©)
project.ext.activeFeatures.each { srcName ->
    def vId = project.ext.dynamicSourceInfoMap[srcName]?.variantId
    if (vId) {
        artifactSuffix += "-${vId}"
    }
}

project.ext.globalArtifactSuffix = artifactSuffix



// --------------------------------------------------------------------------------------
// [Source JAR ë° Fat JAR ìƒì„± ì •ì±… ê²°ì •]
// --------------------------------------------------------------------------------------

// 1. ë°°í¬ ê´€ë ¨ íƒœìŠ¤í¬ ê°ì§€
def taskNames = gradle.startParameter.taskNames
def isAnyPublish = taskNames.any { it.toLowerCase().contains('publish') } // MavenLocal í¬í•¨ ëª¨ë“  ë°°í¬
def isRemotePublish = isAnyPublish && !taskNames.any { it.toLowerCase().contains('mavenlocal') } // ì›ê²© ë°°í¬ë§Œ

// 2. Source JAR ìƒì„± ì—¬ë¶€ ê²°ì • (Task Execution ì‹œì ì— determineSourceJarStatus í˜¸ì¶œë¡œ ì²˜ë¦¬ë¨)
// ì´ˆê¸°í™” ì‹œì ì—ëŠ” ë³„ë„ í˜¸ì¶œ ë¶ˆí•„ìš” (Lazy evaluation or direct call later)

// ì €ì‘ê¶Œ ì—°ë„ ì—…ë°ì´íŠ¸ (ìˆ˜ì •ì´ í•„ìš”í•œ ê²½ìš°ì—ë§Œ íŒŒì¼ IO ë°œìƒ)
S2BuildUtils.updateCopyright(project, [
    'README.md'
] as String[])


// ========================================================================
// â­ [ì„œë¸Œ í”„ë¡œì íŠ¸ êµ¬ì„±] â€» subprojects { ... } ì•ˆì—ì„œì˜ project ëŠ” ì„œë¸Œ í”„ë¡œì íŠ¸ë¥¼ ê°€ë¦¬í‚¨ë‹¤.
// ========================================================================
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    // ì„œë¸Œ í”„ë¡œì íŠ¸ ë³„ Shadow í”ŒëŸ¬ê·¸ì¸ì„ ì ìš©í•œë‹¤. (ë£¨íŠ¸ì˜ ë²„ì „ ì¹´íƒˆë¡œê·¸ì—ì„œ í™•ì¸í•œ ID ì‚¬ìš©).
    // Gradle í”ŒëŸ¬ê·¸ì¸ì€ ì˜ì¡´ì„±ì„ ë²ˆë“¤ë§í•˜ì§€ ì•Šê³  ì„ ì–¸ë§Œ í•´ì•¼í•˜ë¯€ë¡œ ì œì™¸
    if (project.name != 's2-validator-plugin') {
        apply plugin: rootProject.libs.plugins.shadow.get().pluginId
    }

    group = rootProject.group
    if (version == 'unspecified') {
        // ì„œë¸Œ í”„ë¡œì íŠ¸ì— ë²„ì „ì´ ì •ì˜ë˜ì–´ ìˆì§€ ì•Šì€ ê²½ìš° ë£¨íŠ¸ ë²„ì „ì„ ì‚¬ìš©
        version = rootProject.version
    }

    base {
        // archivesName ì—…ë°ì´íŠ¸ (ì ‘ë¯¸ì‚¬ê°€ ìˆëŠ” ê²½ìš°ë§Œ)
        archivesName = "${project.name}${rootProject.ext.globalArtifactSuffix}"
    }

    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java {
                // ì„œë¸Œ í”„ë¡œì íŠ¸ ë””ë ‰í„°ë¦¬ë¥¼ ê¸°ì¤€ìœ¼ë¡œ 'src/main/java'ë¥¼ ì •ì˜í•œë‹¤.
                srcDirs = [rootProject.ext.JAVA_SRC_ROOT]
            }
            resources {
                srcDirs = [rootProject.ext.RESOURCES_SRC_ROOT]
            }
        }
    }

    // í”„ë¡œì íŠ¸ í†µí•© ì„¤ì •
    S2BuildUtils.configureProject(project)

    // ì €ì‘ê¶Œ ì—°ë„ ì—…ë°ì´íŠ¸ (ìˆ˜ì •ì´ í•„ìš”í•œ ê²½ìš°ì—ë§Œ íŒŒì¼ IO ë°œìƒ)
    S2BuildUtils.updateCopyright(project, [
        rootProject.ext.JAVA_SRC_ROOT,
        'src/main/kotlin',
        'src/main/python',
        rootProject.ext.RESOURCES_SRC_ROOT,
        'src/main/webapp',
        'README.md',
        'build.gradle'
    ] as String[])

    /**
     * ì»´íŒŒì¼ëŸ¬ ì˜µì…˜ ì„¤ì •
     */
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        // íŒŒë¼ë¯¸í„°ëª… ì •ìƒì ìœ¼ë¡œ ë³´ì´ë„ë¡ ìˆ˜ì •
        options.compilerArgs += ['-parameters']
        if (options.hasProperty('release')) {
            /**
             * --release ì˜µì…˜ì˜ ì œì•½ì„ í•´ì œí•˜ê³ , êµ¬í˜• ë°©ì‹ì¸ -source ë° -target ì„¤ì •ì„ ê°•ì œë¡œ ì‚¬ìš©
             * Java 21ë¡œ ì»´íŒŒì¼ í•˜ê³  ê²°ê³¼ë¬¼ì„ Java 17ìœ¼ë¡œ ì‹¤í–‰í•  ìˆ˜ ìˆë„ë¡ í•¨
             */
            options.release.set(null)
        }
    }

    /*
     * [ë¼ì´ë¸ŒëŸ¬ë¦¬ ë°°í¬ ì•„í‹°íŒ©íŠ¸ ë° Java í˜¸í™˜ì„± ì„¤ì •]
     *
     * í‘œì¤€ ë°°í¬ íŒŒì¼(Artifacts) ìƒì„±ê³¼ Java í˜¸í™˜ì„± ë ˆë²¨ ì •ì˜
     *
     * 1. ë°°í¬ ì•„í‹°íŒ©íŠ¸:
     * - withSourcesJar(): ì†ŒìŠ¤ íŒŒì¼(*-sources.jar) ìƒì„±
     * - withJavadocJar(): Javadoc ë¬¸ì„œ(*-javadoc.jar) ìƒì„±
     *
     * 2. IDE í™œìš©:
     * - ë©”ì¸ JARì™€ í•¨ê»˜ sources/javadoc JAR ë°°í¬ ì‹œ IDEê°€ ìë™ ê°ì§€í•˜ì—¬ ì—°ê²°
     * - assemble ë˜ëŠ” publish ê³„ì—´ íƒœìŠ¤í¬ë¡œ ìƒì„±ë¨
     *
     * 3. í˜¸í™˜ì„± ì„¤ì •:
     * - sourceCompatibility: ì†ŒìŠ¤ ì½”ë“œ ë ˆë²¨
     * - targetCompatibility: ë°”ì´íŠ¸ì½”ë“œ(.class) ì‹¤í–‰ ë ˆë²¨
     *
     * â˜…â˜…â˜… 'assemble' ì‹¤í–‰ ì‹œ ëª¨ë“  ì£¼ìš” ì•„í‹°íŒ©íŠ¸ ìƒì„± â˜…â˜…â˜…
     *
     * â€» Javadoc ì‘ì„± ì‹œ HTML íƒœê·¸ ì£¼ì˜ (<pre> ê°™ì€ ì‹¤ì œ íƒœê·¸ëŠ” ì‚¬ìš© ê°€ëŠ¥)
     *   - ê¸ˆì§€: <, >, &, {, }, @
     *   - ëŒ€ì²´: List<String> â†’ {@code List<String>}
     */
    java {
        withJavadocJar()
        if (S2BuildUtils.determineSourceJarStatus(project)) {
            withSourcesJar()
        }

        /**
         * [Java Toolchain]
         * ë¹Œë“œ ì‹¤í–‰ í™˜ê²½(JAVA_HOME)ê³¼ í”„ë¡œì íŠ¸ ì»´íŒŒì¼ í™˜ê²½ì„ ë¶„ë¦¬í•˜ëŠ” í˜„ëŒ€ì ì¸ ë°©ì‹
         * 1. ì¼ê´€ì„±: íŒ€ì› ëª¨ë‘ê°€ ë™ì¼í•œ JDK ë²„ì „ìœ¼ë¡œ ë¹Œë“œí•˜ë„ë¡ ê°•ì œ
         * 2. ìë™í™”: ë¡œì»¬ì— í•´ë‹¹ JDKê°€ ì—†ìœ¼ë©´ ì„¤ì •ëœ ë¦¬ì¡¸ë²„(Foojay ë“±)ë¥¼ í†µí•´ ìë™ ë‹¤ìš´ë¡œë“œ
         * 3. ìœ ì—°ì„±: Gradleì€ Java 17ë¡œ ì‹¤í–‰í•˜ë©´ì„œ, í”„ë¡œì íŠ¸ëŠ” Java 21ë¡œ ì»´íŒŒì¼í•˜ëŠ” ë“±ì˜ ì„¤ì •ì´ ê°€ëŠ¥
         */
        toolchain {
            // project.ext.javaVersion (JavaVersion íƒ€ì…)ì—ì„œ ìˆ«ì ë²„ì „ë§Œ ì¶”ì¶œí•˜ì—¬ ì„¤ì •í•¨
            languageVersion = JavaLanguageVersion.of(rootProject.ext.javaVersion.majorVersion)
        }

        // --release ì˜µì…˜ì„ ì œê±°í•˜ì—¬ ì´ ì„¤ì •ë“¤ì´ ì»´íŒŒì¼ëŸ¬ ì¸ì(-source, -target)ë¡œ í™•ì‹¤íˆ ì „ë‹¬ëœë‹¤. (Java 17ìœ¼ë¡œ ì‹¤í–‰í•  ìˆ˜ ìˆë„ë¡ í•¨)
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }


    // publishing ë¸”ë¡
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                // ìƒì„±ë  pom.xml ìƒì„¸ ì„¤ì • (Maven Central í•„ìˆ˜ ìš”ê±´)
                pom {
                    name = project.name
                    description = 'S2Util Library - A comprehensive utility library for Java'
                    url = 'https://github.com/devers2/s2-util'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'devers2'
                            name = 'ì´ìŠ¹ìˆ˜'
                            email = 'eseungsu.dev@gmail.com'
                            organization = 'devers2'
                            organizationUrl = 'https://github.com/devers2'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/devers2/s2-util.git'
                        developerConnection = 'scm:git:ssh://github.com/devers2/s2-util.git'
                        url = 'https://github.com/devers2/s2-util'
                    }
                }
            }
        }
        repositories {
            // GitHub Packages ì„¤ì •
            maven {
                name = 's2-packages'
                url = uri(rootProject.ext.REPO_BASE_URL)
                credentials {
                    username = rootProject.ext.GITHUB_USER
                    password = rootProject.ext.GITHUB_TOKEN
                }
            }
            // Central Portal ë°°í¬ ì„¤ì •
            maven {
                name = "CentralPortal"
                url = "https://central.sonatype.com/api/v1/publisher/deployments/maven2/"
                credentials {
                    username = project.findProperty("centralUsername")
                    password = project.findProperty("centralPassword")
                }
            }
        }
    }

    if (isRemotePublish) {
        // GPG ì„œëª… ì„¤ì •: ë°°í¬(publish) íƒœìŠ¤í¬ ì‹¤í–‰ ì‹œì—ë§Œ ì„œëª… ìˆ˜í–‰
        signing {
            required { gradle.taskGraph.hasTask("publish") }
            sign publishing.publications.mavenJava
        }
    }

    test {
        /**
         * JUnit 5(Jupiter) í”Œë«í¼ ì‚¬ìš© ì„¤ì •.
         * Gradleì€ ê¸°ë³¸ì ìœ¼ë¡œ JUnit 4ë¥¼ ì‚¬ìš©í•˜ë ¤ í•˜ë¯€ë¡œ, JUnit 5 í…ŒìŠ¤íŠ¸ë¥¼ ì‹¤í–‰í•˜ë ¤ë©´ ì´ ì„¤ì •ì´ í•„ìˆ˜ì´ë‹¤.
         */
        useJUnitPlatform()
        // JVM ì¸ì½”ë”© ì„¤ì • (í…ŒìŠ¤íŠ¸ í™˜ê²½ì—ì„œ í•œê¸€ ê¹¨ì§ ë°©ì§€)
        jvmArgs "-Dfile.encoding=UTF-8", "-Dsun.jnu.encoding=UTF-8"
    }

    // â˜…â˜…â˜… 'Tasks â†’ other â†’ copyDependencies' ì‹¤í–‰ ì‹œ ì§€ì • ë””ë ‰í† ë¦¬ë¡œ ì˜ì¡´ì„± ë³µì‚¬ â˜…â˜…â˜…
    S2BuildUtils.registerCopyDependenciesTask(project)
}


// --------------------------------------------------------------------------------------
// ë£¨íŠ¸í”„ë¡œì íŠ¸ README íŒŒì¼ ë²„ì „ & ì˜ì¡´ì„± ê°€ì´ë“œ ì—…ë°ì´íŠ¸
// --------------------------------------------------------------------------------------
S2BuildUtils.updateReadmeWithVersionAndDependencies(project, file('README.md'))


